import _ from "Functor"
import __ from "Show"

export data Maybe a
  = Just a
  | Nothing


instance Functor Maybe {
  map = (f) => where
    is Just x : Just(f(x))
    is Nothing: Nothing
}

instance Show a => Show (Maybe a) {
  show = where
    is Just a : "Just " ++ show(a)
    is Nothing: "Nothing"
}

chain :: (a -> Maybe b) -> Maybe a -> Maybe b
export chain = (f, m) => (where(m) {
  is Just x : f(x)
  is Nothing: Nothing
})

fromMaybe :: a -> Maybe a -> a
export fromMaybe = (or) => where
  is Just a : a
  is Nothing: or
