import W from "Wish"
import IO from "IO"
import S from "System"
import String from "String"
import L from "List"
import Maybe from "Maybe"
import F from "Function"
import { andDo } from "Monad"

testFilePath :: W.Wish String String
testFilePath = chain(
  pipe(
    L.nth(0),
    where
      is Maybe.Just path: of(path)
      is Maybe.Nothing  : W.bad("Test file not given !")
  )
)(S.getArgs)

madlibPath :: W.Wish String String
madlibPath = S.getEnv("MADLIB_PATH") |> W.chainRej((x) => (of("madlib")))

prepare :: W.Wish String <String, String, String>
prepare = pipe(
  W.parallel,
  map(where
    is [entry, madlib]:
      < entry
      , String.replace("(.*\/)(.*).mad$"
      , ".tests/$2.mjs", entry), madlib
      >
  )
)([testFilePath, madlibPath])

prepare
  |> chain(where {
       is <entry, dest, madlib>:
         S.exec(madlib ++ " compile -i " ++ entry ++ " -o .tests/")
           |> andDo(S.spawn("node " ++ dest))
           |> W.mapRej(
                (rej) => where(rej)
                  is S.CommandError _ msg: msg
              )
           |> chain((a) => {
               S.exec("rm -r .tests")
               return of(a)
             })
     })
  |> W.fulfill(IO.err, F.identity)
